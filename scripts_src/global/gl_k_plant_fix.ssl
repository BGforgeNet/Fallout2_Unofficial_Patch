#define SCRIPT_REALNAME "gl_k_plant_fix"
#include "../headers/define.h"
#include "../headers/upu.h"
#include "../sfall/define_extra.h"

procedure gamemode_hook;
procedure get_skynet_ptr;

procedure start begin
  if game_loaded then begin
    register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_hook);
    ndebug("initialized");
  end
end

procedure gamemode_hook begin
  variable old_mode = get_sfall_arg_at(1);
  variable new_mode = get_game_mode;
  variable skynet_ptr = get_skynet_ptr;

  if not (old_mode bwand INTFACELOOT) and (new_mode bwand INTFACELOOT) then begin
    if (loot_obj == skynet_ptr) then begin
      set_proto_data(obj_pid(skynet_ptr), PROTO_CR_BODY_TYPE, CR_BODY_BIPED);
    end
    if (loot_obj == Goris_Ptr) then begin
      set_proto_data(PID_GORIS, PROTO_CR_BODY_TYPE, CR_BODY_BIPED);
    end
  end

  if (old_mode bwand INTFACELOOT) and not (new_mode bwand INTFACELOOT) then begin
    if (skynet_ptr != 0) and (get_proto_data(obj_pid(skynet_ptr), PROTO_CR_BODY_TYPE) == CR_BODY_BIPED) then begin
      set_proto_data(obj_pid(skynet_ptr), PROTO_CR_BODY_TYPE, CR_BODY_ROBOTIC);
    end
    if Goris_In_Party and (get_proto_data(PID_GORIS, PROTO_CR_BODY_TYPE) == CR_BODY_BIPED) then begin
      set_proto_data(PID_GORIS, PROTO_CR_BODY_TYPE, CR_BODY_QUADRUPED);
    end
  end
end

procedure get_skynet_ptr begin
  if Robobrain_Ptr != 0 then return Robobrain_Ptr;
  if Robobrain_Human_Ptr != 0 then return Robobrain_Human_Ptr;
  if Robobrain_Abnormal_Ptr != 0 then return Robobrain_Abnormal_Ptr;
  if Robobrain_Chimp_Ptr != 0 then return Robobrain_Chimp_Ptr;
  return 0;
end
